package proyectocompi;

import java_cup.runtime.*;
import java.io.FileInputStream;
import java.io.InputStream;
import javax.swing.JTextArea;

parser code
{:
    
    public JTextArea outputArea;
    public  int errors = 0;
    
    @Override
    public void syntax_error(Symbol s){
        report_error("Syntax Error: Unexpected Token "+s.value+" at Line "+(s.left+1)+" Column "+(s.right+1), "" );
    }

    @Override
    public void report_error(String message, Object info) {
    if (info instanceof String){
	errors++; 
        outputArea.append("("+errors+")> " + message + info + "\n");
    }
    else {
    	StringBuffer m = new StringBuffer("Error ");
    	if (info instanceof java_cup.runtime.Symbol) 
     	   m.append( "("+info.toString()+")" );     
    	m.append(" : "+message);   
    	System.err.println(m);
    }
  }
   
  @Override
  public void report_fatal_error(String message, Object info) {
    report_error(message, info);
    throw new RuntimeException("Fatal Syntax Error");
  }

    public void setOutput(JTextArea out){
        this.outputArea = out;
    }

:}


//TERMINALES

// Tipos
terminal NUM, BIN, DEC, SYM, STR;

// Valores Con sus Java Types
terminal java.lang.Boolean BOOL;
terminal java.lang.Number DIGIT;
terminal java.lang.Number FLOAT;
terminal java.lang.Character CHAR;
terminal java.lang.String STRING;
terminal java.lang.String ID;

// Statements
terminal END, ASSIGN;
non terminal ID_FUNC_S, STATEMENT_ID_FUNC_S, STATEMENT_ID_FUNC_A;

// Ciclo FOR
terminal REP, REPEND, OUT;
non terminal FOR_S, FOR_A, FOR_B;

// IF
terminal COND, CONDEND, YET, YETEND;
non terminal IF_S, IF_A;


// SWITCH 
terminal SET, SETEND, OPT, ANY;
non terminal SWITCH_S, SWITCH_A, SWITCH_B, SWITCH_C, SWITCH_TYPES;

// WHILE
terminal TIL, TILEND;
non terminal WHILE_S, WHILE_A;

// Main & Funciones
terminal MAIN, MAINEND, ACT, ACTEND, RET, NIL;
non terminal FUNC_S, FUNC_A, FUNC_B, FUNC_C, FUNC_TYPES, FUNC_EXEC_S, FUNC_EXEC_A, FUNC_EXEC_B;

// Operadores 
terminal PLUS, MINUS, MULT, DIV, MOD, DOT;

// Operadores Logicos
terminal AND, OR, LESSTHAN, GREATERTHAN, GREATEREQUALTHAN, LESSEQUALTHAN, EQUAL, NOTEQUAL, NOT;

// Fillers
terminal PARIZQ, PARDER, COMMA, THIS;//, BRAIZQ, BRADER, INCLUDE;

//NO TEMRINALES
non terminal PROGRAM, PROGRAM_BODY;


//Sept No Terminales
non terminal DECLARE_S, DECLARE_A, DECLARE_B, DECLARE_FOR, TYPES, LITERALS, E, T, F;
non terminal LogicalOperators, Concat, Negation, LogicalA, LogicalB;
non terminal ELogical, TLogical, FLogical;

non terminal STATEMENT, STATEMENTS;

start with PROGRAM;

TYPES ::= NUM | DEC | BIN | SYM | STR;
LITERALS ::= BOOL:bl | DIGIT:dt | FLOAT:ft | CHAR:chr | STRING:str | ID:id ID_FUNC_S;
Concat ::= AND | OR;
Negation ::= NOT | ;
LogicalOperators ::= LESSTHAN | GREATERTHAN | LESSEQUALTHAN | GREATEREQUALTHAN | EQUAL | NOTEQUAL;

//Right Side ID or Function Execution Disambiguation
ID_FUNC_S ::= FUNC_EXEC_S | ;
//End Right Side ID or Function

//Statement ID or Function Disambiguation
STATEMENT_ID_FUNC_S ::= ID:id STATEMENT_ID_FUNC_A;
STATEMENT_ID_FUNC_A ::= FUNC_EXEC_S END | ASSIGN E END;
//Statement ID or Function End

//DECLARACIONES
DECLARE_B ::= ID:id COMMA DECLARE_B 
        | ID:id END;
DECLARE_A ::= ID:id ASSIGN E END;
DECLARE_S ::= TYPES DECLARE_A 
        | TYPES DECLARE_B;
//DECLARACIONES END

//DECLARACION PARA EL FOR
DECLARE_FOR ::= TYPES ID:id ASSIGN E;
//CASO ESPECIAL

//ARIT OPERATION
F ::= LITERALS 
    | PARIZQ E PARDER;
T ::= T MULT F 
    | T DIV F 
    | T MOD F 
    | F;
E ::= E PLUS T 
    | E MINUS T 
    | T;
//ARIT OPERATION END


//Logical Operations
LogicalB ::= ELogical LogicalOperators LogicalB 
            | ELogical;
LogicalA ::= LogicalA Concat LogicalB 
            | LogicalB;

//Arithmetic Operations for Logical Statements.
FLogical ::= Negation LITERALS
        | Negation PARIZQ LogicalA PARDER;
TLogical ::= TLogical MULT FLogical 
        | TLogical DIV FLogical 
        | TLogical MOD FLogical 
        | FLogical;
ELogical ::= ELogical PLUS TLogical 
        | ELogical MINUS TLogical 
        | TLogical;

//FUNCTION DECLARATION

FUNC_C ::= RET LITERALS END | ;
FUNC_B ::= COMMA FUNC_A | ;
FUNC_A ::= TYPES ID:id FUNC_B; 
FUNC_TYPES ::= TYPES | NIL;
FUNC_S ::= FUNC_TYPES ACT ID:id PARIZQ FUNC_A PARDER PROGRAM_BODY FUNC_C ACTEND;
//FUNCTION DECLARTION END

//FUNCTION EXECUTION
FUNC_EXEC_B ::= COMMA FUNC_EXEC_A | ;
FUNC_EXEC_A ::= ID:id FUNC_EXEC_B | ;
FUNC_EXEC_S ::= DOT ID:id PARIZQ FUNC_EXEC_A PARDER 
            | PARIZQ FUNC_EXEC_A PARDER;
//FUNCTION EXECUTION END

//IF 
IF_A ::= PROGRAM_BODY CONDEND YET PROGRAM_BODY YETEND 
    | PROGRAM_BODY CONDEND;
IF_S ::= COND PARIZQ LogicalA PARDER IF_A;
// IF END

//FOR 
FOR_B ::= PROGRAM_BODY REPEND | PROGRAM_BODY OUT END REPEND;
FOR_A ::= PARIZQ DECLARE_S LogicalA END DECLARE_FOR PARDER FOR_B;
FOR_S ::= REP FOR_A; 
//FOR END

//WHILE
    WHILE_A ::= TILEND | OUT END TILEND;
    WHILE_S ::= TIL PARIZQ LogicalA PARDER PROGRAM_BODY WHILE_A ;
//WHILE END

//SWITCH 
    SWITCH_TYPES ::= CHAR:ch | DIGIT:dt | ID:id;
    SWITCH_C ::= OUT END | ;
    SWITCH_B ::= SWITCH_B OPT PARIZQ SWITCH_TYPES PARDER PROGRAM_BODY SWITCH_C ;
    SWITCH_A ::= SWITCH_B ANY PROGRAM_BODY SWITCH_C SWITCH_B 
        | SWITCH_B;
    SWITCH_S ::= SET PARIZQ SWITCH_TYPES PARDER SWITCH_A SETEND;
//SWITCH END

STATEMENTS ::= STATEMENTS STATEMENT
            | STATEMENT
            | error {: parser.report_error("Error in a Statement", ""); :}; 

STATEMENT ::= DECLARE_S
            | IF_S
            | FOR_S
            | WHILE_S 
            | SWITCH_S
            | FUNC_S
            | STATEMENT_ID_FUNC_S;

PROGRAM_BODY ::= STATEMENTS | ;

PROGRAM ::= MAIN PROGRAM_BODY MAINEND
        | error PROGRAM_BODY MAINEND {: parser.report_error("RUN END without RUN", ""); :}
        | MAIN error {: parser.report_error("Error Reaching RUN END", ""); :};